## week2

<img src=".\images\1.png" alt="1" style="zoom:60%;" />

### 组合逻辑模型

```verilog
// Verilog IEEE 1364-1995 语法. 
module Simple_Circuit (A, B, C, D, E); 
    output D, E; 
    input A, B, C; 
    wire w1;
	
    and G1 (w1, A, B); 
    not G2 (E, C); 
    or G3 (D, w1, E); 
endmodule
注释：//、/*  */ 
模块名字大小写敏感，只能由字母、数字、下划线组成 端口列表：提供模块与外部环境之间的接口。输入/输出
声明端口类型：input、output、inout
内部连线声明：wire,在SystemVerilog中用logic代替

电路的结构用预定义的原始门来说明： and、or、not
Verilog中有100多个关键字。
```

### 激励模块

```verilog
// Test bench for Simple_Circuit_delay 
module t_Simple_Circuit_delay; //没有输入/输出端口 
    wire D1, E1;     // 电路输出用 wire 
    reg A1, B1, C1; // 电路输入用 reg 
    Simple_Circuit_delay M1 (A1, B1, C1, D1, E1); //实例 
    initial //从仿真0时刻开始执行，只执行一次 
    begin 
        A1 = 1'b0; B1 = 1'b0; C1 = 1'b0; //1位二进制0 
        #100 A1 = 1'b1; B1 = 1'b1; C1 = 1'b1; 
    end 
    initial #200 $finish; //200ns结束 
endmodule
```

### Verilog中的数字

格式：N’Bvalue
				位数 基数 值
		8’h25	 $25_{16} = 0010 0101_2$ 	8位数字，16进制，值为25
		’b 二进制、’o 八进制、’h 十六进制、空：默认十进制
		𝑤 =′ 𝑏11，如果𝑤为8位总线，则𝑤 = 0000 0011

[注] 数字中间 的_下划线可忽略

<img src=".\images\2.png" alt="1" style="zoom:60%;" />

### 布尔函数组合逻辑模型

```verilog
// Verilog model with Boolean expressions 
module Simple_Circuit_Boolean (A, B, C, D, E); 
    output D, E; 
    input A, B, C;
	
    assign D = (A & B) | ~ C; //连续赋值语句 
    assign E = ~ C; 
endmodule
```

assign：连续赋值语句， 等号右边的输入值改变， 等号左边的输出就会随之重新计算。 用于描述组合逻辑，并行执行。 

### Verilog运算符

-  位运算：’b1010 = ~ ’b0101         
- 逻辑运算：if  ! (a > b) 
- 缩位运算符：对单个操作数进行运算，最后返回一位数。 运算过程：首先将操作数的第一位和第二位进行与、或、非运算；然后再将运算结果和第三位进行与、或、非运算；以此类推直至最后一位。 
- 拼接运算符：{s1, s2, …, sn} 
- 重复操作符：形如{{}}，即将一个表达式放入双重花括号中，复制因子放入 第一层括号中，为复制一个常量或变量提供一个简便记法。 例：{3{2'b01}} = 6'b010101。

#### 缩位运算符

```verilog
module and8 (input [7:0] a, output y); 
    assign y = &a; 
    // assign  y = a[7] & a[6] & a[5] & a[4] &a[3] & a[2] & a[1] & a[0]; 
endmodule
```

#### 反相器 (多位向量)

```verilog
//反相器连接4位输入、输出总线 
module inv(input [3:0] a, 
           output [3:0] y); 
    assign y = ~a; 
endmodule
```

<img src=".\images\3.png" alt="1" style="zoom:60%;" />

- a[3:0] 代表一个4位总线。 
- 小端(little-endian) 顺序：a[3]、a[2]、a[1]、a[0] 
- 大端(big-endian) 顺序： a[0]、a[1]、a[2]、a[3] 
- 总线字节顺序是任意选择的，只要保持一致。 

### 自底向上描述4位全加器 (例化)

<img src=".\images\4.png" alt="1" style="zoom:60%;" />

```verilog
module half_adder( output S, C, input x, y); 
// Instantiate primitive gates 
    xor (S, x, y); 
    and (C, x, y); 
endmodule 
```

<img src=".\images\5.png" alt="1" style="zoom:60%;" />

```verilog
module full_adder ( output S, C, input x, y, z); 
    wire S1, C1, C2; 
    // Instantiate half adders 
    half_adder HA1 (S1, C1, x, y); 
    half_adder HA2 (S, C2, S1, z); 
    or G1 (C, C2, C1); 
endmodule 
```

<img src=".\images\6.png" alt="1" style="zoom:60%;" />

```verilog
module adder_4_bit(output [3: 0] Sum, output C4, input [3: 0] A, B, input C0); 
    wire C1, C2, C3; // Intermediate carries 
    // Instantiate chain of full adders 
    full_adder FA0 (Sum[0], C1, A[0], B[0], C0), 
    		   FA1 (Sum[1], C2, A[1], B[1], C1), 
    		   FA2 (Sum[2], C3, A[2], B[2], C2), 
    		   FA3 (Sum[3], C4, A[3], B[3], C3); 
endmodule
```

###  4位加法器(数据流建模)

```verilog
// Dataflow description of four-bit adder 
module binary_adder( 
    output [3: 0] Sum, 
    output C4, 
    input [3: 0]A, B, 
    input C0);
    
    assign {C4, Sum} = A + B + C0; //加法运算后5位结果，C4是进位
endmodule
```

### 阻塞赋值 vs 非阻塞赋值

#### 阻塞赋值 =

```verilog
always @ (posedge clk)
    begin
        c = b;
        b = a;
        $display("blocking: a = %d, b = %d, c = %d.", a, b, c);
    end
```

在clk上升沿到来的时候，把a的值赋给b，再把b的值赋给c，并显示a、b 的值。在把a的值赋给b的这个过程中，其他的语句都“被阻塞”，被迫 停下来，结束之后，进入下一句，直到执行完begin---end中语句。所以相当于把a的值通过b传递给c。

#### 非阻塞赋值 <=

```verilog
always @ (posedge clk)
    begin
        c <= b;
        b <= a;
        $display("non_blocking: a = %d, b = %d, c = %d.", a, b, c);
    end
```

在posedge clk到来时，计算所有的右侧值，此 时，a的值为3，b的值为x，这是同时进行的， 没有先后顺序；然后更新左侧值，结束之后， b的值变为3，c的值为前一时刻b的值，即x。

- 阻塞赋值 “=”：顺序执行。语句的顺序很重要，在本语句中“右式计算”和“左式更新”完全完成之后，才开始执行下一条语句。 

- 非阻塞赋值“<=”：并行执行。同时执行，与顺序无关，当前语句的执行不会阻塞下一语句的执行。

   1) 在开始时，计算所有非阻塞赋值右侧表达式

   2) 在结束时，更新所有非阻塞赋值左侧表达式

#### 使用原则

① 在assign的结构中，必须使用阻塞赋值。 

② 用always块建立组合逻辑模型时，用阻塞赋值。

③ 时序电路(锁存器)建模时，用非阻塞赋值。 

④ 在同一个always块中建立时序和组合逻辑电路时，用非阻塞赋值。

⑤ 在同一个always块中不要既用非阻塞赋值又用阻塞赋值。 

⑥ 不要在一个以上的always块中为同一个变量赋值。

### always语句

```verilog
module mux_2x1_beh (Y, I0, I1, S); 
    output reg Y; 
    input wire I0, I1, S;  // wire 太常用，常省略
	
    always @ (I0 or I1 or S) 
	//敏感信号表：任何信号变化就要重新执行一遍 (注意：always语句末尾没有分号”；”) 
    //always @ (I0, I1, S)  
        if (S == 1) Y = I0; 
    	else Y = I1; 
endmodule
```

过程赋值语句的目标输出必须用reg (寄存器)数据类型，在赋值前保持不变。而不能用wire(连线)。【注】类型reg定义的变量在用赋值语句赋值一个新值之前，会保持数值不变

#### 存储器的HDL描述

```verilog
module memory(
    input Enable, ReadWrite, 
    input [3: 0] DataIn, 
    input [5: 0] Address,   // 2^6 = 64 
    output reg [3: 0] DataOut );  
    // (位宽 字长度) 用二维数组 将存储器 建模成 寄存器阵列 
    reg [3: 0] Mem [63: 0];    // 64 x 4：64个字的存储器，每个字是4位的 
    always @ (Enable or ReadWrite) 
        if (Enable) 
            // 表示：从地址对应的存储器字中读出4位数据送到输出数据线上。 
         	if (ReadWrite) DataOut = Mem [Address];   // Read 
    		// 表示：将输入数据线上的4位数据写入由地址对应的存储器字中。 
    		else Mem [Address] = DataIn;      // Write 
    	else DataOut = 4‘bz;       // z高阻态，存储器不工作 
endmodule
```

### Verilog 中的4种循环语句

**必须用在 initial 或 always语句中**

```verilog
initial 
begin 
    clock = 1'b0; 
    repeat (16) 
        #5 clock = ~ clock; 
end 
//产生8个时钟周期，每个周期是10个单位时间
```

```verilog
initial 
begin 
    clock = 1'b0; 
    forever 
        #10 clock = ~ clock; 
end
//产生20个单位时间
```

```verilog
integer count; 
initial 
begin count = 0; 
    while (count < 64) 
        #5 count = count + 1; 
end
```

```C++
for (j = 0; j < 8; j = j + 1) 
begin 
    // procedural statements 
end
```

### 其它注意事项

- always描述的组合电路一定是电平敏感的。always @ (A, B, S) 
- always描述的时序电路一定是边沿敏感的。always @ (posedge clock)

always语句细化为3种： 

- always_comb：表示组合逻辑的过程 
- always_ff：表示时序逻辑的过程 
- always_latch：表示锁存逻辑的过程

## week3

### ALU运算符

<img src=".\images\7.png" alt="1" style="zoom:60%;" />

通过计算 $S=A-B$ 来实现 $SLT$，如果 $S < 0$ 也就是符号位为 1，则 $Y = 1$，反之为 0。

- 逻辑左移（LSL）或者右移（LSR），以 0 填充空位。

  > 11001 LSR 2 = 00110;	11001 LSL = 00100

- 算术移位器， 与逻辑移位器一样，但算术右移（ASR）会把原来数据的最高有效位填充在新数据的最高有效位上，也就是负数补 1，正数补 0 。算数左移（ASL）与逻辑左移一样。
  
> 11001 ASR 2 = 11110;	11001 ASL 2 = 00100
  
- 循环移位器，循环移动数字，这样从一端移走的位置重新填充到另一端的空位上。

  > 11001 ROR 2 = 01110；	11001 ROL 2 = 00111